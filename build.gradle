plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'com.gorylenko.gradle-git-properties' version '2.4.1'
}

group = 'net.leanstacks'
version = '0.0.1-SNAPSHOT'

defaultTasks 'clean', 'build'

configurations {
	asciidoctorExt
}

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-security'

	implementation 'org.liquibase:liquibase-core'

	runtimeOnly 'org.hsqldb:hsqldb'
	runtimeOnly 'mysql:mysql-connector-java:8.0.33'
	
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

ext {
	snippetsDir = file('build/generated-snippets')
}

bootJar {
	dependsOn asciidoctor

	from ("${asciidoctor.outputDir}") {
		into "static/docs"
	}
}

springBoot {
	buildInfo()
}

tasks.named('test') {
	useJUnitPlatform()

	outputs.dir snippetsDir

	testLogging {
		events "FAILED", "SKIPPED", "PASSED"
	}
}

asciidoctor {
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'
	dependsOn test
}